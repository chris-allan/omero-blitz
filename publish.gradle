task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task pythonZip(type: Zip, dependsOn: "pythonTaskGroup") {
    archiveClassifier.set('python')
    from pythonOutputDir
}

publishing {
    publications {
        bltizJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }

        blitzPython(MavenPublication) {
            artifact pythonZip
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv('ARTIFACTORY_USER') ?: "${findProperty('artifactory_user')}"
                password = System.getenv('ARTIFACTORY_PASSWORD') ?: "${findProperty('artifactory_password')}"
            }
            if (version.endsWith('SNAPSHOT')) {
                url "https://artifacts.openmicroscopy.org/artifactory/ome.snapshots"
            } else {
                url "https://artifacts.openmicroscopy.org/artifactory/ome.staging"
            }
        }
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifactory {
    contextUrl = "http://artifacts.openmicroscopy.org/artifactory"
    publish {
        repository {
            repoKey = 'integration-libs' // The Artifactory repository key to publish to
            username = System.getenv('ARTIFACTORY_USER') ?: "${findProperty('artifactorUser')}"
            password = System.getenv('ARTIFACTORY_PASSWORD') ?: "${findProperty('artifactorPassword')}"
        }
    }
}